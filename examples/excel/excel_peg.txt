formula                         := expression+
expression                       = string_join | arithmetic | comparison | thing
thing                            = function | brackets | string | any_reference | percentage | number | boolean | named_reference | prefix
function                        := /[A-Z]+/ `'(' expression? (`',' expression)* `')'
brackets                        := `'(' expression `')'
string_join                     := thing (`"&" thing)+
arithmetic                      := thing (operator thing)+
comparison                      := thing comparator thing
comparator                      := '>=' | '<=' | '<>' | '>' | '<' | '='
string                          := `'"' /[^"]*/ `'"'
any_reference                    =  table_reference | quoted_sheet_reference | sheet_reference | sheetless_reference
percentage                      := /[-+]?[0-9]+\.?[0-9]*/ `'%'
number                          := /[-+]?[0-9]+\.?[0-9]*([eE][-+]?[0-9]+)?/
operator                        := '+' | '-' | '/' | '*' | '^'
table_reference                  = qualified_table_reference | local_table_reference
qualified_table_reference       := /[a-zA-Z0-9]+/ table_column
local_table_reference           := table_column
table_column                     = `'[' /[^\u005d]*/ `']'
named_reference                 := /[a-zA-Z][\w_.]+/
quoted_sheet_reference          := single_quoted_string `'!' sheetless_reference
sheet_reference                 := /[a-zA-Z][\w_]+/ `'!' sheetless_reference
single_quoted_string             = `"'" /[^']*/ `"'"
sheetless_reference              = column_range | row_range | area | cell
column_range                    := column `':' column
row_range                       := row `':' row
area                            := reference `':' reference
cell                            := reference
row                              = /\$?\d+/
column                           = /\$?[A-Z]+/
reference                        = /\$?[A-Z]+\$?[0-9]+/
boolean                          = boolean_true | boolean_false
boolean_true                    := `'TRUE'
boolean_false                   := `'FALSE'
prefix                          := /[-+]/

